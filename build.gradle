plugins {
    id 'scala'
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

group 'org.enterprisedlt.general'
version '2.0.0-RC1'

compileScala.targetCompatibility = 1.8

dependencies {
    compile 'org.scala-lang:scala-library:2.12.0'


    // testing libraries
    testCompile 'junit:junit:4.12'
    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://dl.bintray.com/enterprisedlt/general' }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: [javadoc, scaladoc]) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId group
            artifactId rootProject.name
            version version
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    publish = true
    pkg {
        repo = 'general'
        userOrg = 'enterprisedlt'
        name = 'contract-spec'
        websiteUrl = 'https://github.com/enterprisedlt/contract-spec'
        vcsUrl = 'https://github.com/enterprisedlt/contract-spec'
        publicDownloadNumbers = true
        licenses = ['LGPL-3.0']
        githubRepo = 'enterprisedlt/contract-spec'
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
}
